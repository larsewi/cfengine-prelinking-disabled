# Policy to disable prelinking

bundle edit_line option_set_no(option)
{
  # Set option value to 'no'
  replace_patterns:
      "^$(option)=.*$"
        replace_with => value("$(option)=no");
}

bundle agent disable_prelinking
{
  # Enable autorun
  meta:
      "tags" slist => { "autorun" };

  # Path to prelink configuration file
  vars:
    redhat::
      "prelink_conf"
        string => "/etc/sysconfig/prelink";
    debian::
      "prelink_conf"
        string => "/etc/default/prelink";
    any::
      "prelink_bin"
        string => "/etc/cron.daily/prelink";

  # Disable prelink in configuration file
  files:
    redhat|debian::
      "$(prelink_conf)"
        edit_line => option_set_no("PRELINKING"),
        classes => if_repaired("prelink_config_file_repaired");

  # Implement changes if prelink configuration file was repaired
  commands:
    prelink_config_file_repaired::
      "$(prelink_bin)";
}
