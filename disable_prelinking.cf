# Policy to disable prelinking
bundle agent disable_prelinking
{
  # Enable autorun
  meta:
    linux::
      "tags" slist => { "autorun" };

  vars:
      "_config_file"
        string => ifelse( "suse|redhat", "/etc/sysconfig/prelink",
                          "debian", "/etc/default/prelink",
                          "/etc/default/prelink");
      "_config[PRELINKING]" string => "no";

  # Disable prelink in configuration file
  files:

      "$(_config_file)" -> { "CCE-27078-5" }
        create => "true",
        edit_line => default:set_line_based("$(this.namespace):$(this.bundle)._config",
                                    " ",
                                    "\s+",
                                    ".*",
                                    "\s*#\s*"),
        classes => default:results( "bundle", "prelink_config_file");

  # Implement changes if prelink configuration file was repaired
  commands:
    prelink_config_file_repaired::
      "/usr/sbin/prelink -ua";
}
