# Policy to disable prelinking

bundle edit_line option_set_no(option)
{
  # Set option value to 'no'
  replace_patterns:
      "^$(option)=(?!no).*$"
        replace_with => value("$(option)=no");
}

bundle agent disable_prelinking
{
  # Enable autorun
  meta:
      "tags" slist => { "autorun" };

  # Disable prelink in configuration file
  files:
      # Redhat systems
      "/etc/sysconfig/prelink"
        edit_line => option_set_no("PRELINKING"),
        classes => if_repaired("prelink_config_file_repaired"),
        if => fileexists("/etc/sysconfig/prelink");

      # Debian systems
      "/etc/default/prelink"
        edit_line => option_set_no("PRELINKING"),
        classes => if_repaired("prelink_config_file_repaired"),
        if => fileexists("/etc/default/prelink");

  # Implement changes if prelink configuration file was repaired
  commands:
    prelink_config_file_repaired::
      "/etc/cron.daily/prelink";
}
